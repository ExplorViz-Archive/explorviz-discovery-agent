apply from: 'https://raw.githubusercontent.com/ExplorViz/gradle-script-plugins/master/microservice.gradle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
  repositories {
    jcenter()	
      maven {
        url 'https://plugins.gradle.org/m2/'
      }
  }

  dependencies {
    classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.3"
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  	
  }
}

repositories {
  jcenter()
  maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {  
  // Common discovery files (e.g. model classes)
  implementation 'com.github.explorviz.explorviz-backend-shared:discovery-common:master-SNAPSHOT'
  
  // Shared utility classes for ExplorViz, e.g., config injection
  implementation 'com.github.explorviz.explorviz-backend-shared:config-injection:master-SNAPSHOT'
  
  // SSE context for injection
  implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-sse', version: '2.27'  
}

assemble.dependsOn shadowJar
jar.enabled = false

jar {
  manifest {
    attributes(
      'Main-Class': 'net.explorviz.discoveryagent.server.main.Main'
    )
  }
}

shadowJar {
  archiveName = "${project.name}.${extension}"
}

spotbugs {
  excludeFilter = file("conf/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = true
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'
  
  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

pmd {
    ruleSets = []
    ruleSetFiles = files("conf/pmd.xml")
    ignoreFailures = true
    toolVersion = "6.7.0"
  }
	
  checkstyle {
    configDir = file("conf")
    configFile = file("conf/checkstyle.xml")
    maxWarnings = 0
    ignoreFailures = true
    toolVersion = "8.12"
  }	
	
  task checkstyle {
    group 'Quality Assurance'
    description 'Run Checkstyle'
    
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
  }
	
  task pmd {
    group 'Quality Assurance'
    description 'Run PMD'
    
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
  }